!% -Cu
!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $ZCODE_LESS_DICT_DATA=1

! The very first lines of the main source code file for a game can
! contain compiler options, like the lines above. -~S disables
! strict error checking. This is otherwise used in z5 and z8 games by
! default. While useful for debugging, it adds ~10 KB to the story file
! size and it makes the game slower.
! $OMIT_UNUSED_ROUTINES=1 makes the compiler remove all routines which
! aren't used. This can save some space.

! ============================================================================ !
!   Cloak of Darkness - a simple demonstration of Interactive Fiction
!	   This version for INFORM written by Roger Firth on 17Sep99
! ============================================================================ !

! When compiling to z8, use the standard library instead
#Iftrue (#version_number == 8);
Constant USEINFORM;
Constant MANUAL_PRONOUNS;
#Endif;

Constant Story	  "Et slør af mørke";
Constant Headline   "^En grundlæggende IF demo.^";
Constant MAX_SCORE  2;

Release 3;
Serial "221116";

Constant STATUSLINE_SCORE; Statusline score;
Constant INITIAL_LOCATION_VALUE = foyer;

[ DeathMessage; print "Du har tabt"; ];


#Ifndef USEINFORM;

Include "globals.h";
Include "puny.h";

#Endif;

#Ifdef USEINFORM;

Include "Parser";
Include "VerbLib";

#Endif;

! ============================================================================ !

Object  foyer "Foyeren i Operahuset"
 with   description
            "Du står i en rummelig foyer, som er pragtfuldt udsmykket i røde og
            gyldne farver med funklende kandelabre i loftet. Indgangen fra
            gaden ligger mod nord, og der er døråbninger mod syd og vest.",
        s_to  bar,
        w_to  garderobe,
        n_to
            "Du er kun lige ankommet, og desuden virker det som om vejret
            udenfor bliver værre og værre.",
 has    light;

Object  -> kasse "kasse"
 with
        name 'kasse' 'kassen',
        inside_description "Det føles virkelig rart at stå i kassen.",
        short_name_def "kassen",
        article  ARTICLE_EN,
 has    container open openable enterable;


Object  -> kamera "kamera"
 with
        name 'kamera' 'kameraet',
        article  ARTICLE_ET,
 has    neuter;

Object  -> aeg "fugleæg"
 with
        name 'fugleæg' 'fugleæggene' 'æg' 'æggene',
        article  ARTICLE_NOGLE,
 has    pluralname;


Object  garderobe "Garderobe"
 with   description
            "Væggene i dette lille rum var øjensynligt engang udstyret med
            rækker af knager, men nu er der blot en enkelt tilbage. Udgangen
            er en dør mod øst.",
        e_to  foyer,
 has    light;

Object  knage "lille messingknage" garderobe
 with   name 'lille' 'messing' 'knage' 'knagen',
        short_name_def "knagen",
        description [;
            print "Det er en lille knage af messing ";
            if (self == parent(sloer)) "med et slør hængende på den.";
            "skruet ind i væggen.";
        ],
 has    scenery supporter;

Object  bar "Foyerens bar"
 with   description
            "Baren, der er mere bart indrettet end den overdådige 
            foyer mod nord, er helt tom. Det ser ud som om en eller 
            anden besked er skriblet i savsmuldet på gulvet.",
        n_to  foyer,
        before [ _test;
         Look, Inv, Going:
            rfalse;
         Go:
#ifdef USEINFORM;
            _test = self hasnt light && noun ~= n_obj;
#IfNot;
            _test = self hasnt light && selected_direction ~= n_to;
#EndIf;
            if (_test) {
                besked.number = besked.number + 2;
                "At famle rundt i mørket er ikke en god ide!";
            }
         default:
            if (self hasnt light) {
                besked.number = besked.number + 1;
                "I mørke? Du kunne nemt komme til at forstyrre noget!";
            }
        ],
 has    ~light;

Object  sloer "silkeslør"
 with   
        parse_name [ _words;
            while(NextWord() == 'smuk' or 'mørk' or 'sort' or 'silke' or 'satin' or 'slør' or 'sløret') _words++;
            return _words;
        ],
        !		name 'smik' 'mørkt' 'sort' 'silke' 'slør' 'sløret',
        description
            "En smuk kappe af fløjl med kanter af satin og med 
            spredte stænk af regndråber. Kappen er så sort i farven, 
            at det næsten ser ud som om, det suger lyset fra rummet.",
        before [;
         Drop, PutOn:
            if (location ~= garderobe)
                "Dette er ikke et passende sted at lade et smukt 
                slør ligge og flyde.";
        ],
        after [;
         Take: give bar ~light;
         Drop, PutOn:
            if (location == garderobe) {
                give bar light;
                if (action == ##PutOn && self has general) {
                    give self ~general;
                    score++;
                }
            }
        ],
 has    neuter clothing general;

Object  besked "skriblet besked" bar
 with   name 'besked' 'beskeden' 'savsmuld' 'gulv',
        description [;
            if (self.number < 2) {
                score++;
                deadflag = 2;
                print "Beskeden, som er omhyggeligt 
                    skriblet i savsmuldet, lyder...";
            }
            else {
                deadflag = 3;
                print "Beskeden er uforsigtigt 
                    blevet betrådt, hvilket har gjort 
                    den svært at tyde. Du kan lige 
                    akkurat tyde ordene...";
            }
        ],
        number  0,
 has    scenery;

[ Initialise;
#Ifdef USEINFORM; ! The location is set with INITIAL_LOCATION_VALUE in PunyInform
    location = foyer;
#Endif;
    move sloer to player;
    give sloer worn;
    "^^Du skynder dig igennem den regnfulde novemberaften og bliver 
    glad, da du ser det strålende lys fra operahuset. Det er 
    pudsigt, at der ikke er flere mennesker på gaden, men hvad havde 
    du egentligt forventet af et billigt demo-spil...?^";
];

! ============================================================================ !

#Ifdef USEINFORM;
Include "Grammar";
#Endif;

Verb 'hæng' * held 'på' noun -> PutOn
    * 'op' held 'på' noun               -> PutOn;

! ============================================================================ !
